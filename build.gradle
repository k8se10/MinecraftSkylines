plugins {
    id 'fabric-loom' version '1.4'
    id 'maven-publish'
    id 'com.github.ben-manes.versions' version '0.51.0'
}

group = 'com.minecraftskylines'
version = '0.0.5a'

base {
    archivesName = 'minecraft-skylines'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

def fabricApiVersion = System.getenv("FABRIC_API_VERSION") ?: "0.97.3+1.20.4"
def yarnMappingsVersion = System.getenv("YARN_MAPPINGS_VERSION") ?: "1.20.4+build.3"
def fabricLoaderVersion = System.getenv("FABRIC_LOADER_VERSION") ?: "0.15.6"
def minecraftVersion = yarnMappingsVersion.split("\\+")[0]

repositories {
    mavenCentral()
    maven { url = "https://maven.fabricmc.net/" }
    maven { url = "https://maven.terraformersmc.com/" }
    maven { url = "https://api.modrinth.com/maven" }
    maven { url = "https://cursemaven.com" }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraftVersion}"
    mappings "net.fabricmc:yarn:${yarnMappingsVersion}:v2"
    modImplementation "net.fabricmc:fabric-loader:${fabricLoaderVersion}"
    modImplementation files("libs/fabric-api-${fabricApiVersion}.jar")
}

loom {
    splitEnvironmentSourceSets()
    mods {
        minecraft_skylines {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

processResources {
    inputs.property "version", project.version
    filteringCharset = "UTF-8"
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}
