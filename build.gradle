plugins {
    id 'fabric-loom' version '1.4+'
    id 'maven-publish'
    id 'com.github.ben-manes.versions' version '0.51.0'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

group = 'com.minecraftskylines'
version = '0.0.5a'
archivesBaseName = 'minecraft-skylines'

repositories {
    mavenCentral()
    maven { url = "https://maven.fabricmc.net/" }
}

def fabricApiVersion = System.getenv("FABRIC_API_VERSION") ?: "0.91.0+1.20.4"
def yarnMappingsVersion = System.getenv("YARN_MAPPINGS_VERSION") ?: "1.20.4+build.3"
def fabricLoaderVersion = System.getenv("FABRIC_LOADER_VERSION") ?: "0.15.6"
def minecraftVersion = yarnMappingsVersion.split("\\+")[0]

dependencies {
    minecraft "com.mojang:minecraft:${minecraftVersion}"
    mappings "net.fabricmc:yarn:${yarnMappingsVersion}:v2"
    modImplementation "net.fabricmc:fabric-loader:${fabricLoaderVersion}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabricApiVersion}"
}

loom {
    splitEnvironmentSourceSets()
    mods {
        minecraft_skylines {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

processResources {
    inputs.property "version", project.version
    filteringCharset = "UTF-8"
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

jar {
    from("LICENSE") {
        rename { "${it}_${archivesBaseName}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}
